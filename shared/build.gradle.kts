import com.mocoding.pokedex.Configuration
import com.mocoding.pokedex.Deps
import org.jetbrains.kotlin.gradle.plugin.mpp.pm20.util.targets

plugins {
    kotlin("multiplatform")
    kotlin("native.cocoapods")
    kotlin("plugin.serialization")
    id("com.android.library")
    id("org.jetbrains.compose")
    id("kotlin-parcelize")
    id("app.cash.sqldelight")
}

@OptIn(org.jetbrains.compose.ExperimentalComposeLibrary::class)
kotlin {
    jvm("desktop")
    android()
    js(IR) { browser() }
    ios()
    iosSimulatorArm64()

    targets.forEach { kotlinTarget ->
        kotlinTarget.compilations.configureEach {
            println(platformType)
            if (name != "jsBrowserDevelopmentRun") {
                compileTaskProvider.configure {
                    println(name)
                    println(this.compilerOptions)
                    println(this.dependsOn)
                    println(this.actions)
                    println(this.taskDependencies)
//                    this.enabled = name != "compileKotlinJs"
                }
            }
//                    compileKotlinTask.enabled = platformType != org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType.js
        }
    }

    cocoapods {
        summary = "Pokedex the Shared Module"
        homepage = "Link to the Shared Module homepage"
        version = "1.0"
        ios.deploymentTarget = "14.1"
        podfile = project.file("../ios/Podfile")
        framework {
            baseName = "shared"
            isStatic = true
        }
    }
    
    sourceSets {
        val commonMain by getting {
            dependencies {
                // Compose
                with(compose) {
                    api(runtime)
                    api(foundation)
                    api(material)
                    api(material3)
                    api(materialIconsExtended)
                }

                // Ktor
                with(Deps.Io.Ktor) {
                    api(ktorClientCore)
                    api(ktorSerializationKotlinxJson)
                    api(ktorClientContentNegotiation)
                    api(ktorClientLogging)
                }

                // Logback for ktor logging
                implementation(Deps.Logback.logbackClassic)

                // SqlDelight
                with(Deps.CashApp.SQLDelight) {
                    api(coroutinesExtensions)
                    api(primitiveAdapters)
                }

                // Koin
                with(Deps.Koin) {
                    api(core)
                    api(test)
                }

                // KotlinX Serialization Json
                implementation(Deps.Org.JetBrains.Kotlinx.kotlinxSerializationJson)

                // Coroutines
                implementation(Deps.Org.JetBrains.Kotlinx.coroutinesCore)

                // MVIKotlin
                with(Deps.ArkIvanov.MVIKotlin) {
                    api(mvikotlin)
                    api(mvikotlinMain)
                    api(mvikotlinExtensionsCoroutines)
                }

                // Decompose
                with(Deps.ArkIvanov.Decompose) {
                    api(decompose)
                    api(extensionsCompose)
                }

                // Image Loading
                api(Deps.Github.imageLoader)
                api(Deps.ArkIvanov.Essenty.lifecycle)
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(kotlin("test"))
            }
        }
        val androidMain by getting {
            dependencies {
                // Ktor
                implementation(Deps.Io.Ktor.ktorClientAndroid)

                // SqlDelight
                implementation(Deps.CashApp.SQLDelight.androidDriver)

                // Koin
                implementation(Deps.Koin.android)
            }
        }
        val androidUnitTest by getting

        val desktopMain by getting {
            dependsOn(commonMain)

            dependencies {
                // Ktor
                implementation(Deps.Io.Ktor.ktorClientJava)

                // SqlDelight
                implementation(Deps.CashApp.SQLDelight.sqliteDriver)
            }
        }

        val jsMain by getting {
            dependsOn(commonMain)

            dependencies {
//                implementation("io.ktor:ktor-client-js:2.2.1")
//                implementation("io.ktor:ktor-client-json-js:2.1.0")
//                implementation(compose.web.core)
//                implementation(compose.runtime)
                // Ktor
                implementation(Deps.Io.Ktor.ktorClientJs)

                // SqlDelight
                api(Deps.ArkIvanov.Essenty.lifecycle)
                implementation(Deps.CashApp.SQLDelight.sqljsDriver)
                implementation(npm("sql.js", "1.6.2"))
                implementation(devNpm("copy-webpack-plugin", "9.1.0"))
            }
        }

        val iosMain by getting {
            dependsOn(commonMain)

            dependencies {
                // Ktor
                implementation(Deps.Io.Ktor.ktorClientDarwin)

                // SqlDelight
                implementation(Deps.CashApp.SQLDelight.nativeDriver)
            }
        }
        val iosSimulatorArm64Main by getting {
            dependsOn(iosMain)
        }
        val iosTest by getting {
            dependsOn(commonTest)
        }
        val iosSimulatorArm64Test by getting {
            dependsOn(iosTest)
        }
    }

    tasks.withType<org.jetbrains.kotlin.gradle.tasks.KotlinCompile> {
        kotlinOptions.jvmTarget = "11"
    }
}

android {
    namespace = "com.mocoding.pokedex"
    compileSdk = Configuration.compileSdk
    defaultConfig {
        minSdk = Configuration.minSdk
        targetSdk = Configuration.targetSdk
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

sqldelight {
    databases {
        create("PokemonDatabase") {
            packageName.set("com.mocoding.pokedex.core.database")
        }
    }
}